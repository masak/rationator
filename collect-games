#!/opt/perl/bin/perl -w
use 5.010;
use strict;

use URI;
use Web::Scraper;
use YAML;
use LWP::UserAgent;

my $player_finder = scraper {
    process 'td>a[href]', 'players[]' => sub {
        my $elem = shift;
        return unless $elem->attr('href') =~ /player\.jsp\?plid=(\d+)/;
        return {
            name => $elem->as_text,
            player_id => $1,
        };
    };
};

my $players = $player_finder->scrape(URI->new(q[http://www.littlegolem.net/jsp/info/player_list.jsp?gtvar=hex_DEFAULT]));

my @best_players = map { $_->{'name'} } @{$players->{'players'}};
my @player_ids = map { $_->{'player_id'} } @{$players->{'players'}};

my $ua = LWP::UserAgent->new;

my %seen_game;
my @games;

for my $player_id (@player_ids) {
    warn $player_id;
    sleep 5;
    my $response = $ua->get('http://www.littlegolem.net/jsp/info/player_game_list_txt.jsp?plid=' . $player_id . '&gtid=hex');

    die $response->status_line unless $response->is_success;

    for my $line (split "\n", $response->content) {
        next if $line =~ m/^ \s* $/xms;

        next if $line !~ m/ RE\[ /xms;

        my ($player_black, $player_white, $winner, $game_id, $moves) =
            $line =~ m/ PB\[(.*?)\]
                        PW\[(.*?)\] .*?
                        RE\[(.*?)\]
                        GC\[\s+ game \s+ \# ([^\]]+)\] .*?
                        SO\[.*?\]
                        (?: ;([^)]+) )? /xms
                or die $line;

        next if $seen_game{$game_id}++;
        next unless $moves;
        next unless
               (grep { $player_black eq $_ } @best_players)
            && (grep { $player_white eq $_ } @best_players);

        my $no_of_moves = 1 + $moves =~ tr/;/;/;

        next if $no_of_moves < 10;

        push @games, {
            player_black => $player_black,
            player_white => $player_white,
            winner       => $winner,
            moves        => $moves,
        };
    }
}

say Dump(\@games);
